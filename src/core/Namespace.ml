open Yuujinchou

type 'a t = 'a Trie.t

let empty = Trie.empty
let add ident sym ns =
  match ident with
  | `User path -> Trie.update_singleton path (fun _ -> Some sym) ns
  | _ -> ns

(* XXX No [failwith]! *)
let nest pp_a modifier imported ns =
  let report_duplicate ~rev_path _old _new =
    Result.error @@ `Duplicate (List.rev rev_path)
  in
  let hooks (`Print lbl) ~rev_prefix t =
    let lbl = Option.fold ~none:"?" ~some:(fun lbl -> "?" ^ lbl) lbl in
    Format.printf "@[<v2>Emitted namespace under %a:@,%s = @[{ "
      Ident.pp (`User (List.rev rev_prefix)) lbl;
    let first = ref true in (* XXX NON-functional programming! *)
    Trie.iteri (fun ~rev_path sym ->
        if not !first then Format.printf "@,; ";
        first := false; (* XXX there are 100 ways to avoid references *)
        Format.printf "@[<hov>%a =>@ %a@]" Ident.pp (`User (List.rev rev_path)) pp_a sym) t;
    Format.printf "@ }@]@]@.@.";
    Result.ok t
  in
  match
    Result.bind
      (Action.run_with_hooks ~hooks ~union:report_duplicate modifier imported)
      (Trie.Result.union report_duplicate ns)
  with
  | Ok merged -> merged
  | Error (`BindingNotFound path) ->
    failwith @@ "no identifiers with the prefix " ^ Ident.to_string (`User path)
  | Error (`Duplicate path) ->
    failwith @@ "duplicate identifiers for " ^ Ident.to_string (`User path)

let find (ident : Ident.t) ns =
  match ident with
  | `User path -> Trie.find_singleton path ns
  | _ -> None
